buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "org.asciidoctor.convert" version "1.5.3"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.asciidoctor.convert' // Apply the Asciidoctor plugin.

sourceCompatibility = 1.8
targetCompatibility = 1.8

bootJar {
    baseName = 'jakduk-api'
    version =  '0.1.0'

    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'public/docs'
    }

    launchScript()
}

// test task 에서 spring.profiles.active 를 위한 설정.
test {
    systemProperties = System.properties as Map<String, ?>
}

asciidoctor {
    dependsOn test
    sourceDir 'src/main/asciidoc'
    attributes \
      'snippets': file('build/snippets')
}

repositories {
    mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-mail')
    compile('org.springframework.boot:spring-boot-starter-data-mongodb')
    compile('org.springframework.boot:spring-boot-starter-data-elasticsearch')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.boot:spring-boot-starter-amqp')
    compile('org.springframework.boot:spring-boot-starter-security')

    compile 'commons-beanutils:commons-beanutils:1.9.3'
    compile 'org.apache.commons:commons-lang3:3.7'
    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'org.apache.commons:commons-text:1.4'
    compile 'com.github.dfabulich:sitemapgen4j:1.0.6'
    compile 'org.jsoup:jsoup:1.11.3'
    compile 'com.rometools:rome:1.10.0'
    compile 'net.coobird:thumbnailator:0.4.8'

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testCompile('org.springframework.security:spring-security-test')
}