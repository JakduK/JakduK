buildscript {
    ext {
        springBootVersion = '2.1.9.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "org.asciidoctor.convert" version "1.5.9.2"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.asciidoctor.convert' // Apply the Asciidoctor plugin.

sourceCompatibility = '1.8'

bootJar {
    baseName = 'jakduk-api'
    version =  '0.1.0'

    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'public/docs'
    }

    launchScript()
}

// test task 에서 spring.profiles.active 를 위한 설정.
test {
    systemProperties = System.properties as Map<String, ?>
}

asciidoctor {
    dependsOn test
    sourceDir 'src/main/asciidoc'
    attributes 'snippets': file('build/snippets')
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-mail')
    implementation('org.springframework.boot:spring-boot-starter-data-mongodb')
    implementation('org.springframework.boot:spring-boot-starter-data-elasticsearch')
    implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
    implementation('org.springframework.boot:spring-boot-starter-amqp')
    implementation('org.springframework.boot:spring-boot-starter-security')
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    implementation 'commons-beanutils:commons-beanutils:1.9.4'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.apache.commons:commons-io:1.3.2'
    implementation 'org.apache.commons:commons-text:1.8'
    implementation 'com.github.dfabulich:sitemapgen4j:1.1.2'
    implementation 'org.jsoup:jsoup:1.12.1'
    implementation 'com.rometools:rome:1.12.2'
    implementation 'net.coobird:thumbnailator:0.4.8'

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation('org.springframework.security:spring-security-test')
}